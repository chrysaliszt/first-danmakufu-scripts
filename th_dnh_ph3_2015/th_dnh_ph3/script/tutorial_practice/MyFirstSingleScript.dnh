#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["My First Danmakufu Single Script!"]
#Text["Code by CHY[r]My First Bullet!"]

#include "script/default_system/Default_ShotConst.txt"

let objBoss;
let objScene = GetEnemyBossSceneObjectID();

@Event {
    alternative(GetEventType())
    case(EV_REQUEST_LIFE) {
        SetScriptResult(500);
    }
    case(EV_REQUEST_TIMER) {
        SetScriptResult(60);
    }
    case(EV_REQUEST_SPELL_SCORE) {
        SetScriptResult(1000000);
    }
}

@Initialize {
    objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    ObjMove_SetDestAtFrame(objBoss, GetCenterX(), 60, 60);

    ObjEnemyBossScene_StartSpell(objScene);

	ObjSound_Play(CreateTrack());
    renderBG;
    TDrawLoop;
    TFinalize;
    TMainTask;
}

@MainLoop {
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);

    yield;
}

task TDrawLoop {
    let imgPath = GetCurrentScriptDirectory() ~ "./u2l13spritesheet.png";
    let aniFrame = 0;
    let motion = 0;

    let previousPos = [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)];
    let currentPos = [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)];

    ObjPrim_SetTexture(objBoss, imgPath);
    ObjRender_SetScaleXYZ(objBoss, 1, 1, 1);
    ObjSprite2D_SetSourceRect(objBoss, 0, 0, 64, 64);
    ObjSprite2D_SetDestCenter(objBoss);

    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
        // Update previous and current position.
        previousPos = currentPos;
        currentPos = [ObjMove_GetX(objBoss), ObjMove_GetY(objBoss)];

        if (currentPos[0] - previousPos[0] + currentPos[1] - previousPos[1] == 0) {
            motion = 0;
        } else {
            motion = 1;
        }

        ObjSprite2D_SetSourceRect(objBoss, 0 + 64 * floor(aniFrame / 8), 0 + 64 * motion, 64 + 64 * floor(aniFrame / 8), 64 + 64 * motion);
        aniFrame++;
        if (aniFrame >= 32) {
            aniFrame = 0;
        }
        yield;
        
    }
}

task TFinalize {
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
        yield;
    }
    if (ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SHOOTDOWN_COUNT) + ObjEnemyBossScene_GetInfo(objScene, INFO_PLAYER_SPELL_COUNT) == 0) {
        AddScore(ObjEnemyBossScene_GetInfo(objScene, INFO_SPELL_SCORE));
    }
    Obj_Delete(objBoss);
    DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
}

task TMainTask {
    wait(120);
    movement;
    fireA;
    fireB;
}

task movement {
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
        ObjMove_SetDestAtFrame(objBoss, rand(GetCenterX() + 90, GetCenterX() - 90), rand(GetCenterY() - 60, GetCenterY() - 120), 60);
        wait(240);
    }
}

task fireA {
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
        let angleT = GetAngleToPlayer(objBoss);
        loop(13) {
            ascent(i in 0..8) {
				if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {return;}
                CreateShotA2(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 0, angleT, 0.01 * (i+1), 20, DS_BALL_S_RED + i, 5);
            }
            angleT += 360/13;
        }
        wait(60);
    }
}

task fireB {
    while(ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
        let angleT = GetAngleToPlayer(objBoss);
        ascent(i in -1..2) {
            ascent(j in 0..6) {
				if(ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {return;}
                let obj = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 4 - j/3, angleT + i*20, DS_RICE_M_SKY, 5);
                ObjRender_SetBlendType(obj, BLEND_ADD_ARGB);
                ObjRender_SetAlpha(obj, 200);
            }
        }
        wait(120);
    }   
}

task renderBG {
    let objBG = ObjPrim_Create(OBJ_SPRITE_2D);
    let imgpath = GetCurrentScriptDirectory() ~ "./RaikoSpellBackground.jpg";
    ObjPrim_SetTexture(objBG, imgpath);

    ObjSprite2D_SetSourceRect(objBG, 0, 0, 384, 448);
    ObjSprite2D_SetDestCenter(objBG);
    ObjRender_SetPosition(objBG, GetCenterX(), GetCenterY(), 0);
    Obj_SetRenderPriorityI(objBG, 20);
}

function CreateTrack() {
	let obj = ObjSound_Create();
	ObjSound_Load(obj, GetCurrentScriptDirectory() ~ "./GhostScapegoat_Sajiboshi.mp3");
	ObjSound_SetSoundDivision(obj, SOUND_BGM);
	ObjSound_SetLoopEnable(obj, true);
	ObjSound_SetLoopTime(obj, 0.1, 219);
	return obj;
}

function GetCenterX() {
    return GetStgFrameWidth() / 2;
}

function GetCenterY() {
    return GetStgFrameHeight() / 2;
}

function wait(frames) {
    loop(frames) {
        yield;
    }
}